plugins {
  id 'io.franzbecker.gradle-lombok' version '1.11'
  id 'java-library'
  id 'maven'
  id 'signing'
}

group = 'com.andonapp'
archivesBaseName = "andonapp"
version = '1.0.0'

sourceCompatibility = 1.8

dependencies {
  api 'com.squareup.okhttp3:okhttp:3.10.0'
  api 'com.fasterxml.jackson.core:jackson-databind:2.9.4'

  testImplementation 'junit:junit:4.12'
  testImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
}

test {
  testLogging {
    events "passed", "skipped", "failed"
  }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'AndonApp Java Client'
        packaging 'jar'
        description 'Java client for connecting to AndonApp.'
        url 'https://www.andonapp.com'

        scm {
          connection 'scm:git@github.com:andonapp/andonapp-java.git'
          developerConnection 'git@github.com:andonapp/andonapp-java.git'
          url 'https://github.com/andonapp/andonapp-java'
        }

        licenses {
          license {
            name 'The MIT License (MIT)'
            url 'https://github.com/andonapp/andonapp-java/blob/master/LICENSE'
          }
        }

        developers {
          developer {
            id 'pwinckles'
            name 'Peter Winckles'
            email 'admin@andonapp.com'
          }
        }
      }
    }
  }
}

repositories {
  mavenCentral()
}
